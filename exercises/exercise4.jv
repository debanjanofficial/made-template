// 1. Pipeline Battery temperature measurements of mobile devices outdoors
pipeline BatteryTempMeasurePipeline {

    BatteryTempMeasureExtractor -> BatteryTempMeasureZipInterpreter;

    BatteryTempMeasureZipInterpreter
        -> BatteryTempMeasureFilePicker
        -> BatteryTempMeasureTextFileInterpreter
        -> BatteryTempMeasureCSVInterpreter
        -> BatteryTempMeasureHeaderRename
        -> BatteryTempMeasureBatteryTemperatureHeaderRename
        -> BatteryTempMeasureTableInterpreter
        -> TemperatureColumnTransformer 
        -> BatteryTemperatureColumnTransformer
        -> BatteryTempMeasureLoader;

    // 2. Extracting file from url
    block BatteryTempMeasureExtractor oftype HttpExtractor{
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block BatteryTempMeasureZipInterpreter oftype ArchiveInterpreter{
        archiveType: "zip";
    }
    // 3. Pick CSV file
    block BatteryTempMeasureFilePicker oftype FilePicker {
        path: "/data.csv";
    }
    
    block BatteryTempMeasureTextFileInterpreter oftype TextFileInterpreter {}
    
    // 3. Interpret CSV file
    block BatteryTempMeasureCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }
    // 4. Rename Header
    block BatteryTempMeasureHeaderRename oftype CellWriter {
        at: range A1:E1;
        write: ['id','producer','model','month','temperature'];
    }
    // 5. Rename Header to battery_temperature
    block BatteryTempMeasureBatteryTemperatureHeaderRename oftype CellWriter {
        at: cell J1;
        write: ['battery_temperature'];
    }
    
    // 7. Transform temperature from Celcius to Fahrenheit
    transform CelciusToFahrenheit {
        from TemperatureInCelcius oftype decimal;
        to TemperatureInFahrenheit oftype decimal;
        TemperatureInFahrenheit: ((TemperatureInCelcius * 9/5) + 32);
    }
    // 8. Transform temperature column
    block TemperatureColumnTransformer oftype TableTransformer {
        inputColumns: ['temperature'];
        outputColumn: 'temperature';
        uses: CelciusToFahrenheit;
    }
    // 9. Transform battery_temperature column
    block BatteryTemperatureColumnTransformer oftype TableTransformer {
        inputColumns: ['battery_temperature'];
        outputColumn: 'battery_temperature';
        uses: CelciusToFahrenheit;
    }
    // 10. Define Range of PosInteger
    valuetype PosInteger oftype integer{
        constraints: [ RangePosInteger];
    }

    constraint RangePosInteger oftype RangeConstraint{
        lowerBound: 0;
        lowerBoundInclusive: false;
    }
    // 10. Define Range of Month
    valuetype ListMonth oftype integer{
        constraints: [ RangeMonth];
    }
    
    constraint RangeMonth oftype RangeConstraint{
        lowerBound: 1;
        lowerBoundInclusive: true;
        upperBound: 12;
        upperBoundInclusive: true;
    }

    // 11. Interpret a Sheet as Table
    block BatteryTempMeasureTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype PosInteger,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype ListMonth,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal,
        ];
    }

    // 12. Loads Table into SQLite Database
    block BatteryTempMeasureLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "temperatures.sqlite";
    }
}